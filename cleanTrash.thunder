##############################################################################
# PROGRAM:    trashCleaner
#
# PURPOSE:    This program is designed to clean a rectangular park of unknown
#             dimensionality (odd/even number of streets, odd/even number of
#             avenues) and deposit all trash in the northeast corner.
#
#             Once the trash is deposited, the robot is to return to the
#             southwest corner, face east, and turn off.
#
#CONSTRAINTS:  
#             turnoff must NOT occur within any bit of logic except as the last
#             physical instruction in the main portion of the program
#
# PROGRAMMER: H. Paul Haiduk
#
# DATE:       October 28, 2016
#
##############################################################################

def turnright:
    do 3:
        turnleft
    #end do
#end turnright

def turnaround:
    turnleft
    turnleft
#end turnaround

def clean_a_row:
    while front_is_clear:
        while next_to_a_beeper:
            pickbeeper
        #end while
        move
        while next_to_a_beeper:
            pickbeeper
        #end while
    #end while
#end clean_a_row

def position_east:
    turnleft
    if front_is_clear:
        move
        turnleft
    #end if
#end position_east

def position_west:
    turnright
    if front_is_clear:
        move
        turnright
    #end if
# end position_west

def clean_park:
    while not_facing_north:
        clean_a_row
        position_east
        if facing_west:
            clean_a_row
            position_west
        #end if
    #end while
#end clean_park

def locate_deposit_corner:
    if right_is_clear:
        turnright
        while front_is_clear:
            move
        #end while
    #end if
#end locate_deposit_corner

def deposit_trash:
    while any_beepers_in_beeper_bag:
        putbeeper
    #end while
#end deposit_trash


def face_west:
    while not_facing_west:
        turnleft
    #end while
#end face_west

def go_home:
    face_west
    while front_is_clear:
        move
    #end while
    turnleft
    while front_is_clear:
        move
    #end while
    turnleft
#end go_home

#######################################################
#######################################################
clean_park
locate_deposit_corner
deposit_trash
go_home
turnoff


